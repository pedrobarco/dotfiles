#!/bin/zsh

_has() {
    which $1>/dev/null 2>&1
}

clone() {
    local url="$1"
    shift 1
    local repo=$(echo "$url" | grep -q "^https://" && echo ${url#*.com/} || echo ${url##*:})
    local repo="${${repo:l}%%.git*}"
    git clone $url "$@" $REPOS/$repo
}

mkrepo() {
    local repo=$(echo $1 | sed "s/\.git//g")
    local repo_path="$REPOS/$repo"

    [ -d $repo_path ] && echo "Repo already exists" && return

    mkdir -p $repo_path
    cd $repo_path > /dev/null
    git init
    cd - > /dev/null
}

rmrepo() {
    local repo=$(echo $1 | sed "s/\.git//g")
    local repo_path="$REPOS/$repo"
    rm -rf $repo_path
}

open_pr() {
    local repo=$(git config --get remote.origin.url | cut -d "@" -f2 | sed "s/.git//g" | sed "s/:/\//g")
    local branch=$(git branch --show-current)

    open "https://${repo}/compare/${branch}?expand=1"
}

force_pull() {
    local branch=$(git branch --show-current)
    git fetch --all
    git reset --hard "origin/${branch}"
}

prune_fetch() {
    local main_branch="main"
    if git show-ref --verify --quiet refs/heads/main; then
        main_branch="main"
    elif git show-ref --verify --quiet refs/heads/master; then
        main_branch="master"
    else
        echo "error: no local 'main' or 'master' branch found."
        return 1
    fi

    # fetch and prune remote branches
    git fetch -p

    # get list of branches that are gone
    local gone_branches=$(git branch -vv | awk '/: gone]/ {print $1}')
    if [ -z "$gone_branches" ]; then
        return 0
    fi

    # delete merged branches
    for b in $gone_branches; do
        if git merge-base --is-ancestor "$b" "$main_branch"; then
            git branch -d "$b"
        fi
    done
}

netshoot() {
    local overrides='{"metadata":{"annotations":{"sidecar.istio.io/inject":"false"}}}'
    local ns="${1:-$(kubectl config view --minify --output 'jsonpath={..namespace}'; echo)}"

    kubectl run netshoot --rm -it \
        --image nicolaka/netshoot:latest \
        --namespace "${ns}" \
        --overrides "${overrides}" \
        -- "/bin/bash"
}
